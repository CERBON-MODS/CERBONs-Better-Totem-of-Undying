import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.1.15'
}

version = "${mc_version}-${mod_version}"
group = "com.cerbon.${mod_id}"
archivesBaseName = "${archive_base_name}-Forge"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {

    mappings channel: 'parchment', version: "${parchment_version}"

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'better_totem_of_undying'

            mods {
                better_totem_of_undying {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'better_totem_of_undying'

            mods {
                better_totem_of_undying {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'better_totem_of_undying'
            mods {
                better_totem_of_undying {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'better_totem_of_undying', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                better_totem_of_undying {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation fg.deobf("curse.maven:voidtotem-449655:${voidtotem_version}")
    implementation fg.deobf("curse.maven:curios-309927:${curios_version}")
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
    config "mixins.${mod_id}.json"
}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = findProperty('CURSEFORGE_TOKEN')

    def mainFile = upload(878676, jar)
    mainFile.changelog = rootProject.file("CHANGELOG.md").getText('UTF-8')
    mainFile.changelogType = 'markdown'
    mainFile.releaseType = 'release'
    mainFile.addGameVersion("${mc_version}", "1.20.1")
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = "Better Totem Of Undying ${project.version}"
    versionNumber = "${project.version}"
    projectId = "qRwFcI3W"
    changelog = rootProject.file("CHANGELOG.md").getText('UTF-8')
    versionType = "release"
    uploadFile = jar
    gameVersions = ["1.20", "1.20.1"]
    loaders = ["forge"]
    syncBodyFrom = rootProject.file("README.md").getText('UTF-8')
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "better_totem_of_undying",
                "Specification-Vendor"    : "better_totem_of_undyingsareus",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "better_totem_of_undyingsareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
